options {
    DEBUG_PARSER = true;
    //FORCE_LA_CHECK = true;
    //DEBUG_LOOKAHEAD = true;
}

PARSER_BEGIN(Example)

package com.github.musiKk.mklang;

import com.github.musiKk.mklang.ast.*;
import java.util.*;

public class Example {
}

PARSER_END(Example)

TOKEN :
{
    <VAR: "var">
    | <VAL: "val">

    | <FOR: "for">

    | <DATA: "data">
    | <TRAIT: "trait">
    | <IMPL: "impl">
    | <FUN: "fun">
    | <ISA: "isa">
}

TOKEN :
{
    <NAME: (["A"-"Z","a"-"z"])+ >
    //| <NUMBER: (["0"-"9"])+ | (["0"-"9"])+ "." (["0"-"9"])+>
    | <NUMBER: (["0"-"9"])+>
    | <STRING: "\"" (~["\""])* "\"">
}

SKIP :
{
    " " | "\t" | "\n"
}

SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])*>
}

CompilationUnit compilationUnit(): {
    CompilationUnit cu = new CompilationUnit();
    Statement stmt;
}
{
    (stmt = statement() { cu.addStatement(stmt); })*
    { return cu; }
}

List<Statement> statements(): {
    List<Statement> ret = new ArrayList<>();
    Statement s;
}
{
    s = statement() { ret.add(s); } (s = statement() { ret.add(s); })*
    { return ret; }
}

Statement statement(): {
    Statement stmt = null;
    Expression expr;
}
{
    (
        stmt = dataDeclaration()
        | traitDeclaration()
        | LOOKAHEAD(implDeclarationExpression()) implDeclarationExpression()
        | LOOKAHEAD(functionDeclaration()) stmt = functionDeclaration()
        | LOOKAHEAD(variableDeclaration()) stmt = variableDeclaration()
        | LOOKAHEAD(variableAssignment()) stmt = variableAssignment()
        | expr = expression() { stmt = new ExpressionStatement(expr); }
    )
    { return stmt; }
}

DataDeclaration dataDeclaration(): {
    String name;
    List<FieldDeclaration> fieldDeclarations = null;
}
{
    <DATA> <NAME> { name = token.image; }
    (
        LOOKAHEAD(1) // hope that's right
        "{"
        (
            "}" | (fieldDeclarations = fieldDeclarations() "}")
        )
    )?
    { return new DataDeclaration(name, fieldDeclarations); }
}

List<FieldDeclaration> fieldDeclarations(): {
    List<FieldDeclaration> decls = new ArrayList<>();
    FieldDeclaration decl;
}
{
    decl = fieldDeclaration() { decls.add(decl); }
    (fieldDeclaration() { decls.add(decl); })*
    { return decls; }
}

FieldDeclaration fieldDeclaration(): {
    String name;
    DottedName type;
}
{
    <NAME> { name = token.image; } ":" type = dottedName()
    { return new FieldDeclaration(name, type); }
}

TraitDeclaration traitDeclaration(): {
    String name;
    List<FunctionSignatureDeclaration> functionSignatureDeclarations = new ArrayList<>();
}
{
    <TRAIT> <NAME> { name = token.image; }
    (
        LOOKAHEAD(1) // hope that's right
        "{"
        (
            "}" | (functionSignatureDeclarations = functionSignatureDeclarations() "}")
        )
    )?
    { return new TraitDeclaration(name, functionSignatureDeclarations); }
}

List<FunctionSignatureDeclaration> functionSignatureDeclarations(): {
    List<FunctionSignatureDeclaration> ret = new ArrayList<>();
    FunctionSignatureDeclaration decl;
}
{
    decl = functionSignatureDeclaration() { ret.add(decl); } (decl = functionSignatureDeclaration() { ret.add(decl); })*
    { return ret; }
}

FunctionSignatureDeclaration functionSignatureDeclaration(): {
    DottedName name;
    DottedName type;
    List<FunctionSignatureDeclaration.Parameter> parameters = new ArrayList<>();
}
{
    <FUN> name = dottedName() "("
    (
        ")" | (parameters = parameters() ")")
    )
    ":"
    type = dottedName()
    { return new FunctionSignatureDeclaration(name, type, parameters); }
}

List<FunctionSignatureDeclaration.Parameter> parameters(): {
    FunctionSignatureDeclaration.Parameter p;
    List<FunctionSignatureDeclaration.Parameter> ret = new ArrayList<>();
}
{
    p = parameter() { ret.add(p); } ("," p = parameter() { ret.add(p); })*
    { return ret; }
}

FunctionSignatureDeclaration.Parameter parameter(): {
    String name;
    DottedName type;
}
{
    <NAME> { name = token.image; } ":" type = dottedName()
    { return new FunctionSignatureDeclaration.Parameter(name, type); }
}

//void implDeclarationExpression(): {}
//{
//    <IMPL> <NAME> <FOR> <NAME>
//    (
//        LOOKAHEAD(1) // hope that's right
//        "{"
//        (
//            "}"
//            | statements() "}"
//        )
//    )?
//}

void implDeclarationExpression(): {}
{
    <NAME> <ISA> dottedName()
}

FunctionDeclaration functionDeclaration(): {
    FunctionSignatureDeclaration signature;
    Expression expression;
}
{
    signature = functionSignatureDeclaration() "=" expression = expression()
    { return new FunctionDeclaration(signature, expression); }
}

VariableDeclaration variableDeclaration(): {
    String name;
    String type = null;
    Expression value;
}
{
    "var" <NAME> { name = token.image; } (":" <NAME> { type = token.image; })? "=" value = expression()
    { return new VariableDeclaration(name, type, value); }
}

VariableAssignment variableAssignment(): {
    String name;
    Expression value;
}
{
    <NAME> { name = token.image; } "=" value = expression()
    { return new VariableAssignment(name, value); }
}

Expression expression(): {
    Expression ret;
}
{
    ret = additiveExpression()
    { return ret; }
}

Expression additiveExpression(): {
    Expression ret;
    Expression right;
}
{
    ret = multiplicativeExpression()
    (
        "+" right = additiveExpression() { ret = new Expression.Additive("+", ret, right); }
    )?
    { return ret; }
}

Expression multiplicativeExpression(): {
    Expression ret;
    Expression right;
}
{
    ret = primary()
    (
        "*" right = multiplicativeExpression() { ret = new Expression.Multiplicative("*", ret, right); }
    )?
    { return ret; }
}

Expression primary(): {
    Expression ret;
}
{
    (
        "(" ret = expression() ")"
        | ret = block()
        //| LOOKAHEAD(methodCallExpression()) methodCallExpression()
        | LOOKAHEAD(functionCallExpression()) ret = functionCallExpression()
        | <NUMBER> { ret = new Expression.IntConstant(Integer.parseInt(token.image)); }
        | dottedName() { ret = null; }
        | <STRING> { ret = new Expression.StringConstant(token.image.substring(1, token.image.length() - 1)); }
    )
    { return ret; }
}

DottedName dottedName(): {
    DottedName dn = new DottedName();
}
{
    <NAME> { dn.addSegment(token.image); }
    ("." <NAME> { dn.addSegment(token.image); })*
    { return dn; }
}

Expression block(): {
    List<Statement> statements;
}
{
    "{"
        statements = statements()
    "}"
    { return new Expression.Block(statements); }
}

//void methodCallExpression(): {}
//{
//    dottedName() "(" (args())? ")"
//}

Expression functionCallExpression(): {
    DottedName name;
    List<Expression> args = null;
}
{
    name = dottedName() "(" (args = args())? ")"
    { return new Expression.FunctionCall(name, args); }
}

List<Expression> args(): {
    List<Expression> ret = new ArrayList<>();
    Expression e;
}
{
    e = arg() { ret.add(e); } ("," e = arg() { ret.add(e); } )*
    { return ret; }
}

Expression arg(): {
    Expression ret;
}
{
    ret = expression()
    { return ret; }
}
